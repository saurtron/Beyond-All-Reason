
#include "../recoil_common_includes.h"

piece  base, thrustfl, thrustfr, thrustbl, thrustbr,  flarefl, flarefr, flarebl, flarebr, 
	bomb3l, bomb3r, bomb2l, bomb2r, bomb1l, bomb1r, bay3l, bay3r, bay2l, bay2r, bay1l, bay1r;

static-var  currbomb;



#define BASEPIECE base
#define HITSPEED <105.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 3
#define MAXTILT 100

#include "../units/unit_hitbyweaponid_and_smoke.h"



Create(){

	turn flarefl to x-axis <-90> now; 
	turn flarefr to x-axis <-90> now; 
	turn flarebl to x-axis <-90> now; 
	turn flarebr to x-axis <-90> now; 

	hide flarefl;
	hide flarefr;
	hide flarebl;
	hide flarebr;
	
	currbomb = 0;
}

Activate()
{
	//get PRINT(1);
	show flarefl;
	show flarefr;
	show flarebl;
	show flarebr;
}

Deactivate()
{
	
	//get PRINT(0);
	hide flarefl;
	hide flarefr;
	hide flarebl;
	hide flarebr;
}

Reload(){
	set-signal-mask 32;
	sleep 3000;
	while (currbomb > 0) {
		currbomb = currbomb -1; 
			if (currbomb == 0) show bomb3l;
			if (currbomb == 1) show bomb3r;
			if (currbomb == 2) show bomb2l;
			if (currbomb == 3) show bomb2r;
			if (currbomb == 4) show bomb1l;
			if (currbomb == 5) show bomb1r;
		sleep 1000;
	}
}

QueryWeapon1(pieceIndex) // called third, and all the fucking time
{
	//get PRINT(3, currbomb);
	pieceIndex = bay3l + currbomb - 1;
	//pieceIndex = bomb3l;
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = base;
}


AimWeapon1(heading,pitch){
	//turn bay3l to y-axis <90> now;
	//turn bomb3l to y-axis <90> now;
	return 1;
}

Shot1(){ // called second, and after each bomb
	//get PRINT(2, currbomb);
	
	if (currbomb == 0) hide bomb3l;
	if (currbomb == 1) hide bomb3r;
	if (currbomb == 2) hide bomb2l;
	if (currbomb == 3) hide bomb2r;
	if (currbomb == 4) hide bomb1l;
	if (currbomb == 5) hide bomb1r;
	
	currbomb = currbomb + 1;
}

FireWeapon1(){ //called first and once only
	//get PRINT(1, currbomb);
	currbomb = 0;
	signal 32;
	start-script Reload();
}



Killed(severity, corpsetype)
{
	// drop all undropped ones
	if (currbomb <=0) explode bomb3l type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2 | NOHEATCLOUD;
	
	
	if (currbomb <=3) explode bomb2r type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2 | NOHEATCLOUD;
	
	if (currbomb <=5) explode bomb1r type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2 | NOHEATCLOUD;

	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
    explode base type BITMAPONLY | NOHEATCLOUD;
	return corpsetype;
}
